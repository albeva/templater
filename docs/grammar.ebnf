(* grammar for template table files *)
Content         = [ StatementList ];
StatementList   = Statement { "\n" Statement };
Statement       = Import | Table;
Import          = "import" STRING "as" IDENTIFIER;

(* table *)
Table            = "table" IDENTIFIER [ "(" TableColumnList ")" ] "=" TableContentList;
TableColumnList  = TableColumn { TableColumn };
TableColumn      = IDENTIFIER [ "=" TableValue ];
TableContentList = TableContent { "+" TableContent };
TableContent     = TableInherit | TableBody;
TableInherit     = Member [ "(" Expression ")" ];
TableBody        = "[" [ TableRowList ] "]";
TableRowList     = TableRow { "\n" TableRow };
TableRow         = TableValue { TableValue };
TableValue       = LITERAL | StructBody;

(* expressions. Incomplete; lacks precedence information *)
Expression       = Primary { BinaryOperator Primary };
Primary          = ( UnaryOperator Primary )
                 | ( "(" Expression ")" )
                 | LITERAL
                 ;
BinaryOperator   = "<" | "<=" | ">" | ">=" | "==" | "!=" | "&&" | "||";
UnaryOperator    = "!";

(* struct *)
StructBody      = "{" [ StructRowList ] "}";
StructRowList   = StructRow { "\n" StructRow };
StructRow       = Member [ TypeSpecifier ] IDENTIFIER [ "=" LITERAL ];
TypeSpecifier   = "*" | "[]";

(* Misc *)
Member          = IDENTIFIER { "." IDENTIFIER };

(* Lex tokens *)
LITERAL         = IDENTIFIER | NUMBER | STRING;
IDENTIFIER      = ( "_" IDENTBODY { IDENTBODY } )
                | ( LETTER { IDENTBODY } )
                ;
IDENTBODY       = DIGIT | LETTER | "_";
NUMBER          = DIGIT { DIGIT };
STRING          = '"' .*? '"';
LETTER          = "A" | "B" | ... | "Z" | "a" | "b" | ... | "z";
DIGIT           = "0" | "1" | ... | "9";
